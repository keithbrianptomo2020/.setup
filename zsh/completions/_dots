#compdef _dots dots

# This is probably the shittiest completion file you can find.
# ------------------------------------------------------------------------------

_commands=(
  'install:Run the installation script for every topic'
  'symlink:Run the symlinking process'
  'list:List all topics'
)

_arg_all=( '(-a --all)'{-a,--all}'[Show all topics]' )
_arg_base_dir=( '(-b --base-dir)'{-b,--base-dir}'[Set the dotfiles directory to run from]:filename:_files' )
_arg_debug_log=( '(-d --debug-log)'{-d,--debug-log}'[Write commands output to debug log]' )
_arg_dry_run=( '(-D --dry-run)'{-D,--dry-run}'[Check the symlinking operations]' )
_arg_exclude=( '(-e --exclude)'{-e,--exclude}'[Exclude topics from installation]' )
_arg_help=( '(-h --help)'{-h,--help}'[Show help output]' )
_arg_yes=( '(-y --yes)'{-y,--yes}'[Skip confirmation questions]' )
_arg_topics=( '*: :($(_get_runnable_topics))' )

_dots_commands() {
  _describe 'command' _commands
}

_dots() {
  local context state state_descr line
  typeset -A opt_args

  _arguments \
    $_arg_help \
    '1: :_dots_commands' \
    '*:: :->command_args'

  case $state in
    command_args)
      case $words[1] in
        install)
          _dots_install
        ;;
        symlink)
          _dots_symlink
        ;;
        list)
          _dots_list
        ;;
      esac
    ;;
  esac
}

# ------------------------------------------------------------------------------

_dots_install() {
  _arguments -C \
    $_arg_yes \
    $_arg_exclude \
    $_arg_debug_log \
    $_arg_base_dir \
    $_arg_topics
}

_dots_symlink() {
  _arguments -C \
    $_arg_base_dir \
    $_arg_dry_run
}

_dots_list() {
  _arguments -C \
    $_arg_all \
    $_arg_base_dir
}

# ------------------------------------------------------------------------------

_get_runnable_topics() {
  find "$DOTFILES" \
    -mindepth 2 \
    -maxdepth 2 \
    -type f \
    -regex '.*/install\(.*\).sh' \
    -exec sh -c 'echo $(basename $(dirname {}))' \;
}

# ------------------------------------------------------------------------------
# vim:ft=zsh:
